# vim: ft=zsh

autoload -Uz add-zsh-hook colors vcs_info

colors
if (( ! $+PROMPT_COLOR )); then
  case $SHORT_HOST in
    elias) PROMPT_COLOR="${fg[red]}" ;;
    esther) PROMPT_COLOR="${fg[cyan]}" ;;
    judah) PROMPT_COLOR="${fg[green]}" ;;
    mordecai) PROMPT_COLOR="${fg[blue]}" ;;
    serenity) PROMPT_COLOR="${fg[cyan]}" ;;
    vashti) PROMPT_COLOR="${fg[blue]}" ;;
  esac
fi

function prompt_willnorris_hook_precmd {
  vcs_info 2>/dev/null
}

function prompt_willnorris {
  (( split = $LINES >= 20 )) # split prompt if sufficient vertical space
  (( trunc = $split == 0 || $COLUMNS < 80 )) # truncate prompt if limited space

  if (( $split )); then
    print ""
  fi
  print -n "%{$PROMPT_COLOR%}"

  if (( $trunc )); then
    if (( $split )); then
      (( cols = $COLUMNS * 3/4))
    else
      (( cols = $COLUMNS * 1/4))
    fi
    print -n "%${cols}<…<%~%<<"
  else
    print -n "%~"
  fi

  if [[ -n ${vcs_info_msg_0_} ]]; then
    print -n " %{${fg[yellow]}%}(${vcs_info_msg_0_})%{$reset_color%}"
  fi

  print -n "%{$PROMPT_COLOR%}"
  if (( $split )); then
    print ""
  else
    print -n " "
  fi
  print -n "%# %{$reset_color%}"
}

function prompt_willnorris_setup {
  PROMPT='$(prompt_willnorris)'

  add-zsh-hook precmd prompt_willnorris_hook_precmd
  add-zsh-hook precmd precmd_set_xterm_title

  zstyle ':vcs_info:*' disable bzr cdv cvs darcs fossil mtn svk tla
  zstyle ':vcs_info:git*' formats "%b%m%u%c"
  zstyle ':vcs_info:*' check-for-changes true
  zstyle ':vcs_info:*' stagedstr   "†"
  zstyle ':vcs_info:*' unstagedstr "‡"
}

prompt_willnorris_setup "$@"
